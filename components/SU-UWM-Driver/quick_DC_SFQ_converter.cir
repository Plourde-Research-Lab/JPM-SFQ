* DC/SFQ Converter Simulation
*
* Author:        Ed Leonard
* Last Modified: Nov 30, 2016
*
* WRSpice circuit to simulate the output of a DC/SFQ converter
* following the layout used in the SFQ_QB project in the group.
*
* The circuit can be found at the following URL:
*
* http://www.physics.sunysb.edu/Physics/RSFQ/Lib/AR/dcsfq.html
*
* Any line including :; anywhere will be skipped by the programmatic element
* labeling utility used as of November 2016.
*
* The following variables need to be replaced in the circuit deck (either by
* hand or programmatically) before running this circuit:
*
*:; I_CRIT_1UM2
*
*:; L_2, L_4, L_5, L_6, L_8, L_10, L_13, L_15
*:; L_PJ2, L_PJ3, L_PJ4, L_PI1
*
*:; JJ_1, JJ_2, JJ_3, JJ_4
*:; RSHUNT_1, RSHUNT_2, RSHUNT_3, RSHUNT_4
*
*:; CURR_START, CURR_STEP, CURR_STOP
*:; I_AMPLITUDE, I_DAMP, I_DELAY, I_FREQ, I_OFFSET

**********************************************
********** BEGIN THE CIRCUIT HERE ************
*** KNOW WHAT YOU ARE DOING BEFORE EDITING ***
**********************************************

* Trigger line
itrig 1 0 sin(0 4.47E-5 5e9 50e-12 0)
rload 1 0 50

* Input to the circuit
l2 5 1 1.39E-11

* Input enters a 3-junction SQUID
* Bottom path
l5 6 5 5.27E-12
l8 0 6 31.0E-12
b3 7 6 jj1 area=4.00E+0
r3 7 6 2.97E+0
lpj3 8 7 2.85E-12
b2 9 8 jj1 area=4.00E+0
r2 9 8 3.68E+0
lpj2 0 9 7.68E-13

* Upper path
l4 10 5 5.38E-12
b1 11 10 jj1 area=3.00E+0
r1 11 10 5.50E+0
l10 11 8 8.78E-13
l6 12 11 4.72E-12
l13 13 12 7.24E-12

* Single cell JTL
b4 14 13 jj1 area=5.50E+0
r4 14 13 2.97E+0
lpj4 14 0 5.49E-13
l15 2 13 8.78E-12

* Bias line
ibias 15 0 8.25E-5
lpi1 12 15 3.29E-13

.tran 0.1e-12 450e-12

.plot v(13) v(1)
* Junction Parameters
* Nb 3 kA/cm2   area = 1 um^2
.model jj1 jj(rtype=1,cct=1,icon=10m,vg=2.8m,delv=0.08m,
+ icrit=1.00E-5,r0=30,rn=30,cap=50f)
.end

.options temp=-272

*.control
*set loopthrds = 4
*set biasCurrAmpStart = 8.25E-5
*set biasCurrAmpStop = 8.25E-5
*set biasCurrStep = 8.25E-5
**loop -$biasVoltAmp $biasVoltAmp $biasVoltStep
*loop $biasCurrAmpStart $biasCurrAmpStop $biasCurrStep

*run

*#write outV2.rawfile v(1) v(12)

*.endc
